buildscript {
    ext.kotlin_version = "1.1.4-3"

    repositories {
        jcenter()
        mavenCentral()
        maven { url "https://plugins.gradle.org/m2/" }
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "gradle.plugin.jaci.openrio.gradle:GradleRIO:2017.2.3"
    }
}

apply plugin: "java"
apply plugin: "kotlin"
apply plugin: "jaci.openrio.gradle.GradleRIO"
deploy.dependsOn "build"
//check.dependsOn "ktlint"

repositories {
    jcenter()
    mavenCentral()
    maven { url 'https://jitpack.io' }
}

configurations {
    ktlint
}

wpi {
    opencvVersion = "3.2.0"
}

frc {
    team = "4099"
    robotClass = "org.usfirst.frc.team4099.robot.Robot"
}

dependencies {

    compile "org.jetbrains.kotlin:kotlin-stdlib-jre8:$kotlin_version"

    compile wpilib()
    compile talonSrx()
    compile navx()

    compile fileTree(dir: 'libs', include: '**/*.jar')
    nativeLib  fileTree(dir: 'libs', include: '**/*.so')

    ktlint 'com.github.shyiko:ktlint:0.9.0'

}

compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}
compileTestKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}


def robotManifest = {
  attributes 'Main-Class': 'edu.wpi.first.wpilibj.RobotBase'
  attributes 'Robot-Class': frc.robotClass
}

jar {
    from configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
    manifest robotManifest
}

task wrapper(type: Wrapper) {
    gradleVersion = '3.2.1'
}

task ktlint(type: JavaExec) {
    main = "com.github.shyiko.ktlint.Main"
    classpath = configurations.ktlint
    args "src/**/*.kt"
}